repositories {
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'net.take'
version = '1.0.3'
def gitHubUrl = 'https://github.com/takenet/TokenAutoComplete'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.BINTRAY_USER
    key = project.BINTRAY_PASSWORD

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'TokenAutoComplete'
        desc = 'Bintray aar publishing example'
        websiteUrl = gitHubUrl
        issueTrackerUrl = gitHubUrl
        vcsUrl = gitHubUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android']
        publicDownloadNumbers = true
    }
}
dependencies {
    compile 'com.android.support:support-annotations:22.0.0'
    compile 'com.android.support:support-v4:22.+'
}
